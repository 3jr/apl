#!/usr/bin/env coffee

# This is an experimental alternative build procedure using the ake project
# https://github.com/ngn/ake

fs = require 'fs'
glob = require 'glob'
assert = require 'assert'
{build, action, coffee, jade, sass, docco, cat} = require '../ake/lib/ake'

libFiles =
  glob.sync('src/*.coffee').map (f) ->
    f.replace /^src\/(.+)\.coffee$/, 'lib/$1.js'

build(
  coffee 'src/*.coffee', 'lib/'
  coffee 'test/doctest.coffee'
  docco  'src/*.coffee', 'docs'

  # web demo
  coffee 'web/index.coffee'
  jade   'web/index.jade'
  sass   'web/index.sass'
  cat(
    [
      'web/fake-require.js'
      libFiles
      'web/jquery*.js'
      'web/examples.js'
      'web/index.js'
    ]
    'web/all.js'
    transform: (content, {path}) ->
      if not path.match /^lib\// then return content
      if path is 'lib/command.js' then return ''
      moduleName = path.replace /^.*\/([^\/]+).js/, '$1'
      """
        defModule('./#{moduleName}', function (exports, require) {
          #{content}
          return exports;
        });\n
      """
  )

  # mobile demo
  coffee 'm/index.coffee'
  jade   'm/index.jade'
  sass   'm/index.sass'
)
