⍬←0⍴0
⍝ ⍬     <=> 0⍴0
⍝ ⍴⍬    <=> ,0
⍝# ⍬←5   !!!
⍝ ⍳0    <=> ⍬
⍝ ⍴0    <=> ⍬
⍝ ⍬     <=> ⍬
⍝ ⍬⍬    <=> ⍬ ⍬
⍝ 1⍬2⍬3 <=> 1 ⍬ 2 ⍬ 3

⊢←{⍵}
⍝ 123⊢456 <=> 456
⍝ ⊢456 <=> 456

⊣←{}⍠{⍺}
⍝ 123⊣456 <=> 123
⍝ ⊣456 <=> ⍬

≢←{⍬⍴(⍴⍵),1}⍠{~⍺≡⍵}
⍝ ≢0 <=> 1
⍝ ≢0 0 0 <=> 3
⍝ ≢⍬ <=> 0
⍝ ≢2 3⍴⍳6 <=> 2
⍝ 3≢3 <=> 0

⌹←{
  norm←{(⍵+.×+⍵)*0.5}

  QR←{ ⍝ QR decomposition
    n←(⍴⍵)[1]
    1≥n:{t←norm,⍵ ⋄ (⍵÷t)(⍪t)}⍵
    m←⌈n÷2
    a0←((1↑⍴⍵),m)↑⍵
    a1←(0,m)↓⍵
    (q0 r0)←∇a0
    c←(+⍉q0)+.×a1
    (q1 r1)←∇a1-q0+.×c
    (q0,q1)((r0,c)⍪((⌊n÷2),-n)↑r1)
  }

  Rinv←{ ⍝ Inverse of an upper triangular matrix
    1=n←1↑⍴⍵:÷⍵
    m←⌈n÷2
    ai←∇(m,m)↑⍵
    di←∇(m,m)↓⍵
    b←(m,m-n)↑⍵
    bx←-ai+.×b+.×di
    (ai,bx)⍪((⌊n÷2),-n)↑di
  }

  0=⍴⍴⍵:÷⍵
  1=⍴⍴⍵:,∇⍪⍵
  2≠⍴⍴⍵:↗'ASSERTION ERROR'
  0∊≥/⍴⍵:↗'ASSERTION ERROR'
  (Q R)←QR ⍵
  (Rinv R)+.×+⍉Q
}⍠{
  (⌹⍵)+.×⍺
}
⍝ ⌹2 <=> .5
⍝ ⌹2 2⍴4 3 3 2 <=> 2 2⍴¯2 3 3 ¯4
⍝ (4 4⍴12 1 4 10 ¯6 ¯5 4 7 ¯4 9 3 4 ¯2 ¯6 7 7)⌹93 81 93.5 120.5 <=>
⍝ ... .0003898888816687221 ¯.005029566573526544 .04730651764247189 .0705568912859835
